plugins {
	id 'java-library'
	id "eclipse"
	id "maven-publish"
	id 'org.cadixdev.licenser' version '0.6.1'
	id "org.ajoberstar.grgit" version "4.1.1"
	id "checkstyle"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def ENV = System.getenv()

def version_tag = 'dtklib_version'
def artifact_name = 'dtklib'

project.archivesBaseName = artifact_name

if (grgit == null) {
	project.version = '99.0.0-nogit'
} else if (project.hasProperty(version_tag)) {
	if (grgit.status().isClean()) {
		project.version = project.getProperty(version_tag) + '.' + grgit.log().size()
	} else {
		project.version = project.getProperty(version_tag) + '.' + (grgit.log().size() + 1) + '-snapshot'
	}

	if (grgit.branch.current().name != 'main') {
		artifact_name = artifact_name + '-' + grgit.branch.current().name
		project.archivesBaseName = artifact_name
	}
} else {
	project.version = '99.0.0-local'
}

license {
	header rootProject.file("HEADER")
	include "**/*.java"
}

checkstyle {
	configFile = rootProject.file("checkstyle.xml")
	toolVersion = "8.44"
}

def eclipseName = artifact_name;

eclipse {
	project {
		name = eclipseName;
	}
}

tasks.withType(JavaCompile) {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

jar {
	from "LICENSE"
	manifest
			{
				attributes "Implementation-Title": project.archivesBaseName
				attributes "Implementation-Version": project.version
				attributes "Maven-Artifact": "${group}:${project.archivesBaseName}:${project.version}"
				attributes "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
			}
}

java {
	withSourcesJar()
}

sourceSets {
	main {
		java {
			include '**/*'
			exclude '*.DS_Store'
		}
		resources
				{
					include '**/*'
					exclude '*.DS_Store'
				}
	}
	test {
		java {
			exclude '**/*'
		}
	}
}

publishing {
	publications {
        mavenJava(MavenPublication) {
            artifactId = artifact_name
            from components.java
            pom {
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 or later'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0-standalone.html'
                    }
                }
                developers {
                    developer {
                        id = 'grondag'
                        name = 'grondag'
                    }
                }
            }
        }
    }

	repositories {
		mavenLocal()
        maven {
            url = 'sftp://maven.vram.io:22'
            credentials {
            	if (project.hasProperty('maven_user')){
					username = project.getProperty('maven_user')
				}
				if (project.hasProperty('maven_password')){
					password = project.getProperty('maven_password')
				}
            }
        }
	}
}

apply from: 'dependencies.gradle'
